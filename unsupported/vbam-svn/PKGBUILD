# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgbase=vbam-svn
pkgname=('vbam-sdl-svn' 'vbam-gtk-svn' 'vbam-wx-svn')
pkgver=1.8.0.r1444
pkgrel=1
pkgdesc='Nintendo GameBoy Advance emulator'
arch=('i686' 'x86_64')
url='http://vba-m.com'
license=('GPL2')
makedepends=('cmake' 'desktop-file-utils' 'ffmpeg' 'gtkglextmm' 'sdl' 'sfml'
             'subversion' 'wxgtk2.8')
makedepends_i686=('nasm')
source=('vbam::svn+http://svn.code.sf.net/p/vbam/code/trunk')
sha256sums=('SKIP')

pkgver() {
  cd vbam

  printf "1.8.0.r%s" "$(svnversion | tr -d '[A-z]')"
}

prepare() {
  cd vbam

  sed 's/wxrc/wxrc-2.8/g' -i {po/update_pot.sh,src/wx/CMakeLists.txt}
  sed 's|Common/|common/|' -i src/gba/debugger-expr-yacc.cpp
}

build() {
  cd vbam

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  _cmakeargs="-DCMAKE_BUILD_TYPE='Release' \
              -DCMAKE_INSTALL_PREFIX='/usr' \
              -DCMAKE_SKIP_RPATH='TRUE' \
              -DENABLE_GTK='TRUE' \
              -DENABLE_WX='TRUE' \
              -DENABLE_FFMPEG='TRUE' \
              -DENABLE_LINK='TRUE' \
              -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' \
              -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'"

  if [[ $CARCH == i686 ]]; then
    _cmakeargs="${_cmakeargs} \
                -DENABLE_ASM_CORE='TRUE' \
                -DENABLE_ASM_SCALERS='TRUE'"
  fi

  cmake .. ${_cmakeargs}
  make
}

package_vbam-sdl-svn() {
depends=('sdl' 'sfml')
provides=('vbam-sdl')
conflicts=('vbam-sdl')
backup=('etc/vbam.cfg')

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin/{g,wx}vbam,share}
}

package_vbam-gtk-svn() {
depends=('desktop-file-utils' 'gtkglextmm' 'sdl' 'sfml')
provides=('vbam-gtk')
conflicts=('vbam-gtk' 'vbam-wx' 'vbam-wx-svn')
install='vbam-gtk.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/bin/vbam}
  find "${pkgdir}" -type f -regex .*wxvbam.* -exec rm {} +
}

package_vbam-wx-svn() {
depends=('ffmpeg' 'sfml' 'wxgtk2.8')
provides=('vbam-wx')
conflicts=('vbam-gtk' 'vbam-gtk-svn' 'vbam-wx')
options=('!emptydirs')
install='vbam-wx.install'

  cd vbam/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,share}/vbam}
  find "${pkgdir}" -type f -regex .*gvbam.* -exec rm {} +
}

# vim: ts=2 sw=2 et:
