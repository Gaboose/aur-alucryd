# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Marc Schulte <bomba@nerdstube.de>

pkgbase=tlp-git
pkgname=('tlp-git' 'tlp-rdw-git')
pkgver=0.7.r41.6f7bca9
pkgrel=1
arch=('any')
url='http://linrunner.de/en/tlp/docs/tlp-linux-advanced-power-management.html'
license=('GPL2' 'GPL3')
makedepends=('git')
source=('tlp::git+https://github.com/linrunner/TLP.git#branch=devel'
        '0001-Modify-hardcoded-paths-via-Makefile.patch'
        '0002-Add-un-install-rule-for-manpages.patch'
        '0003-Use-TLP_PLIB-in-tlp-nop.patch')
sha256sums=('SKIP'
            'f3e49ebb857197678e265d4b5c9bd42780ea65530ea73a670c9d04c8210b3dde'
            '4424a21668586d34c76dd42b56d0469e881629e99ebfce6ad1c3fe07cb26b1c7'
            'ae21a2e8ee11bc316fe246ff7b10f01d7003fddd02037befac1b0d07dbefccee')

pkgver() {
  cd tlp

  tag='0.7'

  echo "${tag}.r$(git rev-list --count ${tag}..HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd tlp

  patch -Np1 -i ../0001-Modify-hardcoded-paths-via-Makefile.patch
  patch -Np1 -i ../0002-Add-un-install-rule-for-manpages.patch
  patch -Np1 -i ../0003-Use-TLP_PLIB-in-tlp-nop.patch
}

build() {
  cd tlp

  make
}

package_tlp-git() {
  pkgdesc='Linux Advanced Power Management'
  depends=('hdparm' 'iw' 'pciutils' 'rfkill' 'usbutils' 'util-linux')
  optdepends=('acpi_call: Sandy Bridge and newer ThinkPad battery functions'
              'ethtool: Disable Wake On Lan'
              'lsb-release: Display LSB release version in tlp-stat'
              'smartmontools: Display S.M.A.R.T. data in tlp-stat'
              'tp_smapi: ThinkPad battery functions'
              'x86_energy_perf_policy: Set energy versus performance policy on x86 processors')
  provides=('tlp')
  conflicts=('laptop-mode-tools' 'pm-utils' 'tlp')
  backup=('etc/default/tlp')

  cd tlp

  export TLP_SBIN='/usr/bin'
  export TLP_ULIB='/usr/lib/udev'
  export TLP_SYSD='/usr/lib/systemd/system'
  export TLP_NO_INIT='1'
  export TLP_NO_PMUTILS='1'
  export TLP_WITH_SYSTEMD='1'

  make DESTDIR="${pkgdir}" install-tlp install-man
}

package_tlp-rdw-git() {
  pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
  depends=('networkmanager' 'tlp')
  provides=('tlp-rdw')
  conflicts=('tlp-rdw')

  cd tlp

  make DESTDIR="${pkgdir}" install-rdw
}

# vim: ts=2 sw=2 et:
