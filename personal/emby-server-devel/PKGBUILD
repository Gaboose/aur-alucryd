# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server-devel
pkgver=3.0.5713.6
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=('i686' 'x86_64' 'armv6h')
url='http://emby.media'
license=('GPL2')
depends=('ffmpeg' 'imagemagick' 'libmediainfo' 'mono' 'sqlite')
provides=('emby-server')
conflicts=('emby-server')
install='emby-server.install'
_commit='ec3fdf3deec25491869648709e351271c9d64b0b'
source=("git+https://github.com/MediaBrowser/Emby.git#commit=${_commit}"
        'emby-server'
        'emby-server.conf'
        'emby-server.service')
backup=('etc/conf.d/emby-server')
sha256sums=('SKIP'
            '7a9c7859ce715a3d8474b0122aa3cff2791cc3695f5d098b12917e559d992081'
            '064a82fb13816af5eb2a90bc8320d062ead0bf700715ee65df6afb347f890907'
            '4a6606e32cd40c3bc5671c673611e51ebdf9b48024991510c02a1c678a430934')

prepare() {
  cd Emby

  sed 's/libMagickWand-6.Q8.so/libMagickWand-6.Q16HDRI.so/' -i MediaBrowser.Server.Mono/ImageMagickSharp.dll.config
}

build() {
  cd Emby

  xbuild \
    /p:Configuration='Release Mono' \
    /p:Platform='Any CPU' \
    /p:OutputPath="${srcdir}/emby-server" \
    /t:build MediaBrowser.Mono.sln
}

package() {
  install -dm 755 "${pkgdir}"/{etc/conf.d,usr/{bin,lib/systemd/system}}
  cp -dr --no-preserve='ownership' emby-server "${pkgdir}"/usr/lib/
  install -m 755 emby-server "${pkgdir}"/usr/bin/
  install -m 644 emby-server.service "${pkgdir}"/usr/lib/systemd/system/
  install -m 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server

  install -dm 755 "${pkgdir}"/var/lib/emby
  chown 422:422 -R "${pkgdir}"/var/lib/emby
}

# vim: ts=2 sw=2 et:
