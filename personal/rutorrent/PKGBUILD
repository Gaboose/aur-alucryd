# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Piotr Rogo≈ºa <rogoza.piotr@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Sergei Lebedev <superbobry@gmail.com>

pkgname=rutorrent
pkgver=3.7
pkgrel=1
pkgdesc='Yet another web front-end for rTorrent'
arch=('any')
url='https://github.com/Novik/ruTorrent'
license=('GPL')
depends=('coreutils' 'curl' 'gzip' 'php')
backup=('etc/webapps/rutorrent/config.php'
        'etc/webapps/rutorrent/access.ini'
        'etc/webapps/rutorrent/plugins.ini')
source=("https://bintray.com/artifact/download/novik65/generic/ruTorrent-${pkgver}.zip")
sha256sums=('c1fb86317231c8e8fa9e1a7dea450ce4687d94caad22d763bc68d90c56d0749f')

package() {
  install -dm 755 "${pkgdir}"/{etc,usr/share}/webapps
  cp -dr --no-preserve='ownership' ruTorrent-master "${pkgdir}"/usr/share/webapps/rutorrent
  mv "${pkgdir}"/usr/share/webapps/rutorrent/conf "${pkgdir}"/etc/webapps/rutorrent

  ln -s /etc/webapps/rutorrent "${pkgdir}"/usr/share/webapps/rutorrent/conf

  chown http:http -R "${pkgdir}"/usr/share/webapps/rutorrent
}

# vim: ts=2 sw=2 et:
