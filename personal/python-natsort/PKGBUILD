# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chris Warrick <aur@chriswarrick.com>
# Contributor: Yaron de Leeuw <me@jarondl.net>
pkgbase=python-natsort
pkgname=('python-natsort' 'python2-natsort')
pkgver=4.0.4
pkgrel=1
pkgdesc='Natural sorting for python'
arch=('any')
url='https://github.com/SethMMorton/natsort'
license=('MIT')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=("natsort-${pkgver}.tar.gz::https://github.com/SethMMorton/natsort/archive/v${pkgver}.tar.gz")
sha256sums=('7ef1ce94e0aea215f6d3a6ce9d2fc82a6c749778ac91765bd1d97c5f6b465889')

prepare() {
  cp -r {,python2-}natsort-${pkgver}
}

build() {
  cd natsort-${pkgver}

  python setup.py build

  cd ../python2-natsort-${pkgver}

  python2 setup.py build
}

package_python-natsort() {
  depends=('python')

  cd natsort-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python-natsort
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python-natsort/
}

package_python2-natsort() {
  depends=('python2')

  cd python2-natsort-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  mv "${pkgdir}"/usr/bin/natsort{,2}

  install -dm 755 "${pkgdir}"/usr/share/licenses/python2-natsort
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python2-natsort/
}

# vim: ts=2 sw=2 et:
