# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-mgba-git
pkgver=r4304.1e95e891
pkgrel=1
pkgdesc='Nintendo Game Boy Advance core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/mgba'
license=('custom:MPL2')
groups=('libretro-unstable')
depends=('glibc')
makedepends=('git')
provides=('libretro-mgba')
conflicts=('libretro-mgba')
source=('libretro-mgba::git+https://github.com/libretro/mgba.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mgba_libretro.info')
sha256sums=('SKIP'
            '82a7bafc29274aaed66fdab50f40b0759668d7114166a7f3bb70a538f67fb28b')

pkgver() {
  cd libretro-mgba

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-mgba

  make -f Makefile.libretro
}

package() {
  cd libretro-mgba

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mgba_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mgba_libretro.info "${pkgdir}"/usr/share/libretro/info/

  install -dm 755 "${pkgdir}"/usr/share/licenses/libretro-mgba-git
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/libretro-mgba-git/
}

# vim: ts=2 sw=2 et:
