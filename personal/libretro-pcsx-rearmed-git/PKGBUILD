# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-pcsx-rearmed-git
pkgver=r1230.f82bcc6
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=('x86_64')
url='https://github.com/libretro/pcsx_rearmed'
license=('GPL2')
groups=('libretro-unstable')
depends=('glibc' 'zlib')
makedepends=('git')
provides=('libretro-pcsx-rearmed')
conflicts=('libretro-pcsx-rearmed')
source=('libretro-pcsx-rearmed::git+https://github.com/libretro/pcsx_rearmed.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/pcsx_rearmed_libretro.info')
sha256sums=('SKIP'
            'd9357cce679fed91a9f59021bbd19fb6d9dbc18e337b8db3ccf1913fb03457c9')

pkgver() {
  cd libretro-pcsx-rearmed

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-pcsx-rearmed

  export LDFLAGS="$LDFLAGS -ldl"

  make -f Makefile.libretro
}

package() {
  cd libretro-pcsx-rearmed

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 pcsx_rearmed_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../pcsx_rearmed_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
