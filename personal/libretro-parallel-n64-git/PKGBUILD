# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-parallel-n64-git
pkgver=r4483.cba0a179
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/parallel-n64'
license=('GPL2')
groups=('libretro-unstable')
depends=('gcc-libs' 'glibc' 'libgl')
makedepends=('git')
provides=('libretro-parallel-n64')
conflicts=('libretro-parallel-n64')
source=('libretro-parallel-n64::git+https://github.com/libretro/parallel-n64.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/parallel_n64_libretro.info')
sha256sums=('SKIP'
            'cb66b26f68412da5b03e755cc1bd82c684d2023b57dc001c397539af6f91295c')

pkgver() {
  cd libretro-parallel-n64

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-parallel-n64

  [[ $CARCH = 'i686' ]] && make WITH_DYNAREC='x86'
  [[ $CARCH = 'x86_64' ]] && make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-parallel-n64

  install -Dm 644 parallel_n64_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../parallel_n64_libretro.info -t "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
