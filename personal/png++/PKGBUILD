# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: SÃ¡ndor Nagy <sanya868@gmail.com>
# Contributor: Jeramy Harrison <dbralir@gmail.com>
# Contributor: Onni R. <onnir@iki.fi>

pkgname=png++
pkgver=0.2.7
pkgrel=1
pkgdesc='A C++ wrapper for libpng'
arch=('any')
url='http://savannah.nongnu.org/projects/pngpp/'
license=('custom')
depends=('libpng')
makedepends=('doxygen')
source=("http://download.savannah.gnu.org/releases/pngpp/png++-${pkgver}.tar.gz")
sha256sums=('2dd70d6c5b953546c392d1c11f38a2ad53dafb72015b0b0c075a1697f67f27b3')

build() {
  cd png++-${pkgver}

  make docs
}

package() {
  cd png++-${pkgver}

  make PREFIX="${pkgdir}/usr" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/png++
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/png++/
}

# vim: ts=2 sw=2 et:
