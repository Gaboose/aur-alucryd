# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile.desu@gmail.com>

pkgname=vapoursynth-git
pkgver=R28.r37.f29c53e
pkgrel=1
pkgdesc='A video processing framework with the future in mind'
arch=('i686' 'x86_64')
url='http://www.vapoursynth.com/'
license=('LGPL2.1' 'custom:OFL')
depends=('gcc-libs' 'glibc' 'libass' 'python' 'tesseract'
         'libavutil.so' 'libswscale.so')
makedepends=('cython' 'git' 'python-sphinx' 'yasm' 'zimg')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git+https://github.com/vapoursynth/vapoursynth.git'
        'vapoursynth.xml')
sha256sums=('SKIP'
            '8e51579547d20cd7cb9618a47b3ac508423d09d76649bf038d0ab9acb850b068')

pkgver() {
  cd vapoursynth

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
  cd vapoursynth

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package() {
  cd vapoursynth

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/{licenses/vapoursynth-git,mime/packages}
  install -m 644 ofl.txt "${pkgdir}"/usr/share/licenses/vapoursynth-git/
  install -m 644 ../vapoursynth.xml "${pkgdir}"/usr/share/mime/packages/
}

# vim: ts=2 sw=2 et:
