# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ffmpeg-alucryd
pkgver=2.8.2
pkgrel=1
epoch=1
pkgdesc='complete, cross-platform solution to record, convert and stream audio and video'
arch=('i686' 'x86_64')
url='http://ffmpeg.org/'
license=('GPL' 'custom')
depends=('alsa-lib' 'bzip2' 'dcadec' 'fontconfig' 'freetype2' 'fribidi' 'glibc'
         'gmp' 'gnutls' 'gsm' 'jack' 'lame' 'libass' 'ladspa' 'libbluray'
         'libmodplug' 'libpulse' 'libsoxr' 'libssh' 'libtheora' 'libva'
         'libvdpau' 'libwebp' 'libx11' 'libxcb' 'libxext' 'libxv'
         'opencore-amr' 'openjpeg' 'opus' 'schroedinger' 'sdl' 'speex'
         'v4l-utils' 'xz' 'zlib'
         'libavcodec.so' 'libavdevice.so' 'libavfilter.so' 'libavformat.so'
         'libavresample.so' 'libavutil.so' 'libfdk-aac.so' 'libpostproc.so'
         'libswresample.so' 'libswscale.so' 'libvidstab.so' 'libvorbisenc.so'
         'libvorbis.so' 'libvpx.so' 'libx264.so' 'libx265.so' 'libxvidcore.so')
makedepends=('hardening-wrapper' 'libvdpau' 'yasm')
provides=('ffmpeg'
          'libavcodec.so' 'libavdevice.so' 'libavfilter.so' 'libavformat.so'
          'libavresample.so' 'libavutil.so' 'libpostproc.so' 'libswresample.so'
          'libswscale.so')
conflicts=('ffmpeg')
source=("http://ffmpeg.org/releases/ffmpeg-${pkgver}.tar.bz2"
        'libvpxenc-remove-some-unused-ctrl-id-mappings.patch')
sha256sums=('830ec647f7ad774fc0caf17ba47774bf5dee7a89cbd65894f364a87ba3ad21b2'
            '54d0996af14b6c3b9cef6cc127ce1cebefc434481368991e40f9f6e2f9f703ee')

prepare() {
  cd ffmpeg-${pkgver}

  patch -Np1 -i ../libvpxenc-remove-some-unused-ctrl-id-mappings.patch
}

build() {
  cd ffmpeg-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-debug \
    --disable-static \
    --disable-stripping \
    --enable-avisynth \
    --enable-avresample \
    --enable-fontconfig \
    --enable-gnutls \
    --enable-gpl \
    --enable-ladspa \
    --enable-libass \
    --enable-libbluray \
    --enable-libdcadec \
    --enable-libfdk-aac \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libgsm \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopencore_amrnb \
    --enable-libopencore_amrwb \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-libpulse \
    --enable-libschroedinger \
    --enable-libsoxr \
    --enable-libspeex \
    --enable-libssh \
    --enable-libtheora \
    --enable-libv4l2 \
    --enable-libvidstab \
    --enable-libvorbis \
    --enable-libvpx \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-nonfree \
    --enable-shared \
    --enable-version3 \
    --enable-x11grab

  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,server}.1
}

package() {
  cd ffmpeg-${pkgver}

  make DESTDIR="${pkgdir}" install install-man
  install -m 755 tools/qt-faststart "${pkgdir}"/usr/bin/

  install -dm 755 "${pkgdir}"/usr/share/licenses/ffmpeg-alucryd
  ln -s ../libfdk-aac "${pkgdir}"/usr/share/licenses/ffmpeg-alucryd/
}

# vim: ts=2 sw=2 et:
