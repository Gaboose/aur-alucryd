# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: xantares <xantares09@hotmail.com>

pkgname=mingw-w64-cmake
pkgver=2
pkgrel=1
pkgdesc='CMake wrapper for MinGW'
depends=('cmake' 'mingw-w64-gcc' 'mingw-w64-pkg-config')
arch=('any')
url='http://fedoraproject.org/wiki/MinGW'
license=('GPL')
source=('mingw-cmake.in'
        'toolchain-mingw.cmake')
sha256sums=('901f7928ccee3410aca54d4093ad7de75d00971057521970642f8d4cbbb9d672'
            '688d28492483f83bf80d49d96254189a49bf2450bb2e1d3047bf945b5f730100')

build() {
  for _triple in {i686,x86_64}-w64-mingw32; do
    sed "s|@TRIPLE@|${_triple}|g" mingw-cmake.in > ${_triple}-cmake
    sed "s|@TRIPLE@|${_triple}|g" toolchain-mingw.cmake > toolchain-${_triple}.cmake
  done
}

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,share/mingw}
  for _triple in {i686,x86_64}-w64-mingw32; do
    install -m 755 ${_triple}-cmake "${pkgdir}"/usr/bin/
    install -m 644 toolchain-${_triple}.cmake "${pkgdir}"/usr/share/mingw/
  done
}

# vim: ts=2 sw=2 et:
