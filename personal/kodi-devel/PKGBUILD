# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: BlackIkeEagle <ike.devolder@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.vex@gmail.com>
# Contributor: Zeqadious <zeqadious@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=kodi-devel
pkgver=16.0a1
_codename='Jarvis'
pkgrel=1
pkgdesc='Open Source Home Theatre Software'
arch=('i686' 'x86_64')
url='http://kodi.tv/'
license=('GPL2')
depends=('bluez-libs' 'curl' 'glew' 'hicolor-icon-theme' 'lame' 'libaacs'
         'libass' 'libbluray' 'libcdio' 'libmariadbclient' 'libmicrohttpd'
         'libmodplug' 'libmpeg2' 'libpulse' 'libsamplerate' 'libssh' 'libva'
         'libvdpau' 'libvorbis' 'libxrandr' 'libxslt' 'lzo' 'mesa-demos'
         'python2-pillow' 'python2-simplejson' 'rtmpdump' 'sdl2' 'smbclient'
         'taglib' 'tinyxml' 'xorg-xdpyinfo' 'yajl'
         'libavcodec.so' 'libavfilter.so' 'libavformat.so' 'libavutil.so'
         'libpostproc.so' 'libswresample.so' 'libswscale.so')
makedepends=('afpfs-ng' 'boost' 'cmake' 'doxygen' 'ffmpeg' 'git' 'glew' 'gperf'
             'jasper' 'java-runtime' 'libcec' 'libgl' 'libnfs' 'libplist'
             'nasm' 'shairplay' 'swig' 'unzip' 'upower' 'zip')
optdepends=('afpfs-ng: Apple shares support'
            'bluez: Bluetooth support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'libnfs: NFS shares support'
            'libplist: AirPlay support'
            'lirc: Remote controller support'
            'lsb-release: Log distro information in crashlog'
            'pulseaudio: PulseAudio support'
            'shairplay: AirPlay support'
            'udisks: Automount external drives'
            'unrar: Archives support'
            'unzip: Archives support'
            'upower: Display battery level')
provides=('kodi')
conflicts=('kodi')
options=('!emptydirs')
install='kodi.install'
source=("https://github.com/xbmc/xbmc/archive/${pkgver}-${_codename}.tar.gz")
sha256sums=('ec20732275d67fd50b1c63434599fa7858d93a3d4829f56bce0d6fa2bf550717')

prepare() {
  cd xbmc-${pkgver}-${_codename}

  find -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i {} +
}

build() {
  cd xbmc-${pkgver}-${_codename}

  export PYTHON_VERSION=2

  ./bootstrap
  ./configure \
    --prefix='/usr' \
    --exec-prefix='/usr' \
    --with-ffmpeg='shared' \
    --with-lirc-device='/run/lirc/lircd' \
    ac_cv_type__Bool='yes'

  make
}

package() {
  cd xbmc-${pkgver}-${_codename}

  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/bin/xbmc{,-standalone}
  rm "${pkgdir}"/usr/share/xsessions/xbmc.desktop
}

# vim: ts=2 sw=2 et:
