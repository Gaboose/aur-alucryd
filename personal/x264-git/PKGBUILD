# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264-git
pkgver=r2893.b00bcafe
pkgrel=1
pkgdesc='A free software library and application for encoding video streams into the H.264/MPEG-4 AVC compression format'
arch=('x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL2')
depends=('glibc'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'libffms2.so'
         'liblsmash.so' 'libswscale.so')
makedepends=('ffms2' 'git' 'l-smash' 'nasm')
provides=('libx264' 'x264'
          'libx264.so')
conflicts=('libx264' 'x264')
source=('git+https://git.videolan.org/git/x264.git')
md5sums=('SKIP')

pkgver() {
  cd x264

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd x264

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --enable-lto \
    --disable-avs
  make
}

package() {
  cd x264

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
