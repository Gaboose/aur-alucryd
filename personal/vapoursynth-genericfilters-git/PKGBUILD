# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-genericfilters-git
pkgver=r115.95bc18a
pkgrel=1
pkgdesc='Generic filters for Vapoursynth'
arch=('i686' 'x86_64')
url='https://github.com/myrsloik/GenericFilters'
license=('LGPL2.1')
depends=('vapoursynth')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}/src

  ./configure --install="${pkgdir}/usr/lib/vapoursynth"
  make
}

package(){
  cd ${pkgname%-*}/src

  make install

  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/generic-filters
  install -m 644 ../readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/generic-filters/
}

# vim: ts=2 sw=2 et:
