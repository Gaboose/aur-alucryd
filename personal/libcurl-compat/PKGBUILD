# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Evan Anderson <evananderson@thelinuxman.us>
# Contributor: Christian Hesse <arch@eworm.de>
# Contributor: Piotr Balcerowski <piotr@balcerowski.org>

pkgname=libcurl-compat
pkgver=7.49.1
pkgrel=3
pkgdesc='An URL retrieval library (without versioned symbols)'
arch=('i686' 'x86_64')
url='http://curl.haxx.se'
license=('MIT')
depends=('ca-certificates' 'glibc' 'krb5' 'libidn' 'openssl' 'zlib'
         'libssh2.so')
options=('strip')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha256sums=('ff3e80c1ca6a068428726cd7dd19037a47cc538ce58ef61c59587191039b2ca6'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd curl-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-libidn \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/libcurl{,-compat}.so.4.4.0
  rm "${pkgdir}"/usr/lib/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0; do
    ln -s libcurl-compat.so.4.4.0 "${pkgdir}"/usr/lib/libcurl.so.${version}
  done

  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libcurl-compat/
}

# vim: ts=2 sw=2 et:
