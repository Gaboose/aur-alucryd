# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus-git
pkgver=r608.eda9d168
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/mupen64plus-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl' 'libpng' 'zlib'
         'libm.so')
makedepends=('git' 'mesa')
provides=('libretro-mupen64plus')
conflicts=('libretro-mupen64plus')
source=('libretro-mupen64plus::git+https://github.com/libretro/mupen64plus-libretro.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mupen64plus_libretro.info')
sha256sums=('SKIP'
            'a1d6b1190a3024b41bc8eb0b590bcdd651fd9cc3deb42c535d52ce3ab356cfee')

pkgver() {
  cd libretro-mupen64plus

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-mupen64plus

  [[ $CARCH = 'i686' ]] && make WITH_DYNAREC='x86'
  [[ $CARCH = 'x86_64' ]] && make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-mupen64plus

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mupen64plus_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mupen64plus_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
