# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Yuval Adam <yuval@y3xz.com>

pkgname=dotnet-coreclr
pkgver=1.1.0
pkgrel=1
pkgdesc='The .NET Core runtime'
arch=('x86_64')
url='https://github.com/dotnet/coreclr'
license=('MIT')
depends=('icu' 'libunwind' 'lldb' 'lttng-ust')
makedepends=('clang' 'cmake' 'llvm' 'python2')
source=("dotnet-coreclr-${pkgver}.tar.gz::https://github.com/dotnet/coreclr/archive/v${pkgver}.tar.gz"
        'dotnet-coreclr-clang39.patch')
sha256sums=('edc1e416f07a71e2b3f70c1f1412e45a7396b3f0daac5bcb267d5f779b9d7444'
            '1360b2b238e4d4a325dc138ccaf94666d99a96f3707a155d1f94945a83436ae0')

prepare() {
  cd coreclr-${pkgver}

  patch -Np1 -i ../dotnet-coreclr-clang39.patch
}

build() {
  cd coreclr-${pkgver}

  export PYTHON='/usr/bin/python2'

  ./build.sh x64 release
}

package() {
  install -dm 755 "${pkgdir}"/opt/dotnet-cli/shared/Microsoft.NETCore.App
  cp -dr --no-preserve='ownership' coreclr-${pkgver}/bin/Product/Linux.x64.Release "${pkgdir}"/opt/dotnet-cli/shared/Microsoft.NETCore.App/1.1.0
  find "${pkgdir}" -type f -name '*.dbg' -delete
}

# vim: ts=2 sw=2 et:
