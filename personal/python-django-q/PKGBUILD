# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=python-django-q
pkgname=('python-django-q' 'python2-django-q')
pkgver=0.7.18
pkgrel=1
pkgdesc='A multiprocessing distributed task queue for Django'
arch=('any')
url='https://github.com/Koed00/django-q'
license=('MIT')
makedepends=('python2-django' 'python2-setuptools' 'python2-six'
             'python-django' 'python-setuptools' 'python-six')
source=(django-q-${pkgver}.tar.gz::"https://github.com/Koed00/django-q/archive/v${pkgver}.tar.gz")
sha256sums=('fdc6b4c64c85d8a7fa332f876a7db582a753667f125efa0a12811d886e2b2349')

prepare() {
  cp -r {,python-}django-q-${pkgver}
  mv {,python2-}django-q-${pkgver}
}

build() {
  cd python-django-q-${pkgver}

  python setup.py build

  cd ../python2-django-q-${pkgver}

  python2 setup.py build
}

package_python-django-q() {
  depends=('python-arrow' 'python-blessed' 'python-django'
           'python-django-picklefield' 'python-future' 'python-six')

  cd python-django-q-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-django-q
}

package_python2-django-q() {
  depends=('python2-arrow' 'python2-blessed' 'python2-django'
           'python2-django-picklefield' 'python2-future' 'python2-six')

  cd python-django-q-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python2-django-q
}

# vim: ts=2 sw=2 et:

