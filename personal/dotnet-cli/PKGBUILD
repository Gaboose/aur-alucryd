# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=dotnet-cli
pkgver=1.0.0_preview2_1_003177
pkgrel=1
pkgdesc='The .NET Core command-line tools'
arch=('x86_64')
url='https://github.com/dotnet/cli'
license=('MIT')
#depends=('dotnet-coreclr' 'dotnet-corefx')
source=("dotnet-cli-${pkgver//_/-}.tar.gz::https://dotnetcli.blob.core.windows.net/dotnet/preview/Binaries/${pkgver//_/-}/dotnet-dev-fedora.24-x64.${pkgver//_/-}.tar.gz")
sha256sums=('4ff0b843f00f89b4391cb2b442f973117d676c56b571b40c9ffa49af3740f717')

_coreclr_files=(
  'libclrjit.so'
  'libcoreclr.so'
  'libcoreclrtraceptprovider.so'
  'libdbgshim.so'
  'libmscordaccore.so'
  'libmscordbi.so'
  'libsos.so'
  'libsosplugin.so'
  'System.Globalization.Native.so'
  'sosdocsunix.txt'
)

_corefx_files=(
  'System.IO.Compression.Native.so'
  'System.Native.a'
  'System.Native.so'
  'System.Net.Http.Native.so'
  'System.Net.Security.Native.so'
  'System.Security.Cryptography.Native.OpenSsl.so'
  )

package() {
  for file in ${_coreclr_files[@]}; do
    rm shared/Microsoft.NETCore.App/1.1.0/$file
  done

  for file in ${_corefx_files[@]}; do
    rm shared/Microsoft.NETCore.App/1.1.0/$file
  done

  install -dm 755 "${pkgdir}"/{usr/bin,opt/dotnet-cli}
  cp -dr --no-preserve='ownership' dotnet host sdk shared "${pkgdir}"/opt/dotnet-cli/
  ln -s /opt/dotnet-cli/dotnet "${pkgdir}"/usr/bin/

  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/dotnet-cli/
}

# vim: ts=2 sw=2 et:
