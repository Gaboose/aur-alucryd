# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-fluxsmooth-git
pkgver=1.0.r4.8f81346
pkgrel=1
pkgdesc='FluxSmooth plugin for VapourSynth'
arch=('x86_64')
url='https://github.com/dubhater/vapoursynth-fluxsmooth'
license=('None')
depends=('vapoursynth')
makedepends=('git')
provides=('vapoursynth-plugin-fluxsmooth')
conflicts=('vapoursynth-plugin-fluxsmooth')
source=('vapoursynth-plugin-fluxsmooth::git+https://github.com/dubhater/vapoursynth-fluxsmooth.git')
sha256sums=('SKIP')

pkgver() {
  cd vapoursynth-plugin-fluxsmooth

  echo "$(git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./')"
}

build() {
  cd vapoursynth-plugin-fluxsmooth

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-plugin-fluxsmooth

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth/README
}

# vim: ts=2 sw=2 et:
