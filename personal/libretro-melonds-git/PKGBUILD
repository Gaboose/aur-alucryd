# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-melonds-git
pkgver=r315.4130dfe
pkgrel=1
pkgdesc='Nintendo DS core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/melonDS'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
provides=('libretro-melonds')
conflicts=('libretro-melonds')
source=('libretro-melonds::git+https://github.com/libretro/melonDS.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/melonds_libretro.info')
sha256sums=('SKIP'
            '75dccee87bfaba67884963dfb1171fd106219e6f757a8aae297270f9bd2d964b')

pkgver() {
  cd libretro-melonds

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-melonds

  make
}

package() {
  cd libretro-melonds

  install -Dm 644 melonds_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../melonds_libretro.info -t "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
