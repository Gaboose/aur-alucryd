# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik.schwan@linux.com>

pkgname=gitea
pkgver=1.5.1
pkgrel=2
pkgdesc='A painless self-hosted Git service'
arch=('x86_64')
url='http://gitea.io'
license=('MIT')
depends=('git')
makedepends=('go-pie' 'go-bindata')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
source=("git+https://github.com/go-gitea/gitea.git#tag=v${pkgver}"
        'gitea.tmpfiles'
        'gitea.service'
        'gitea-ldflags.patch'
        'gitea-disable-u2f.patch')
sha256sums=('SKIP'
            '4bcddd5d05ce17968866beb5690517710e040554c9a555a7af06ca549be8c3b0'
            '4bc8d49f6eb79d881cfb124a0be7e5c1336bfd0e1842fab98d76972c2a0d2e03'
            '8d02a510dc7d8d27e0a0ac67e8a8a2bf7868c1cc494426969bd2b4d6d74e71ac')

prepare() {
  mkdir -p src/code.gitea.io
  ln -s "${srcdir}"/gitea src/code.gitea.io/

  cd gitea
  patch -Np1 -i ../gitea-ldflags.patch
  patch -Np1 -i ../gitea-disable-u2f.patch
}

build() {
  cd src/code.gitea.io/gitea
  GOPATH="${srcdir}" make generate
  GOPATH="${srcdir}" TAGS="bindata sqlite pam" make build
}

package() {
  install -Dm 755 gitea/gitea -t "${pkgdir}"/usr/bin/
  install -Dm 644 gitea/LICENSE -t "${pkgdir}"/usr/share/licenses/gitea/
  install -Dm 644 gitea.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 gitea.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/gitea.conf
  install -dm 775 -o root -g git "${pkgdir}"/etc/gitea
  install -m 664 -o root -g git gitea/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}

# vim: ts=2 sw=2 et:
