# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-temporalsoften-git
pkgver=1.0
pkgrel=1
pkgdesc='TemporalSoften plugin for VapourSynth'
arch=('i686' 'x86_64')
url='https://github.com/dubhater/vapoursynth-temporalsoften'
license=('None')
depends=('vapoursynth')
makedepends=('git')
provides=('vapoursynth-plugin-temporalsoften')
conflicts=('vapoursynth-plugin-temporalsoften')
source=('vapoursynth-plugin-temporalsoften::git+https://github.com/dubhater/vapoursynth-temporalsoften.git')
sha256sums=('SKIP')

pkgver() {
  cd vapoursynth-plugin-temporalsoften

  echo "$(git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./')"
}

build() {
  cd vapoursynth-plugin-temporalsoften

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-plugin-temporalsoften

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/TemporalSoften
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/TemporalSoften/README
}

# vim: ts=2 sw=2 et:
