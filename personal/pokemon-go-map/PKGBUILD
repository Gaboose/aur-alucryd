# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Grey Christoforo <grey@christoforo.net>

pkgname=pokemon-go-map
pkgver=2.0.0
pkgrel=3
pkgdesc='Live visualization of all the pokemon in your area... and more!'
arch=('any')
url='https://jz6.github.io/PoGoMap/'
license=('AGPL3')
depends=('python2-click' 'python2-flask' 'python2-flask-cors' 'python2-geopy'
         'python2-gpsoauth' 'python2-itsdangerous' 'python2-jinja'
         'python2-markupsafe' 'python2-peewee' 'python2-protobuf'
         'python2-protobuf-to-dict' 'python2-requests' 'python2-s2sphere'
         'python2-werkzeug' 'python2-wsgiref')
backup=('etc/conf.d/pokemon-go-map')
install='pokemon-go-map.install'
source=("https://github.com/AHAAAAAAA/PokemonGo-Map/archive/v${pkgver}.tar.gz"
        'pokemon-go-map'
        'pokemon-go-map.conf'
        'pokemon-go-map.service')
sha256sums=('9897cd3816e231025f94b2ac8f7896dec45cd8738583cb8dfc8c0664b990beab'
            '553e77806ec5cdbeb1ff42bbf1b99a37685f1b9d309af2cc9db1b6927806db44'
            '3521d8d86bbcc4f8635c182aff6b7e38737a9886862c95e59d61d3788923c414'
            '006512eaa2d4f473c9ce020f690676f20c63763f5484f0db9ddd7ab5a4049334')

package() {
  install -dm 755 "${pkgdir}"/{etc/conf.d,usr/{bin,lib/systemd/system},opt}
  cp -dr --no-preserve='ownership' PokemonGo-Map-${pkgver} "${pkgdir}"/opt/pokemon-go-map
  install -m 755 pokemon-go-map "${pkgdir}"/usr/bin/pokemon-go-map
  install -m 644 pokemon-go-map.conf "${pkgdir}"/etc/conf.d/pokemon-go-map
  install -m 644 pokemon-go-map.service "${pkgdir}"/usr/lib/systemd/system/

  install -dm 755 "${pkgdir}"/var/lib/pokemon-go-map
  chown 721:721 "${pkgdir}"/var/lib/pokemon-go-map
}

# vim: ts=2 sw=2 et:
