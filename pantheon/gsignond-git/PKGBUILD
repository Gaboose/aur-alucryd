# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic@gmail.com>

pkgname=gsignond-git
pkgver=1.0.6.r12.38bbf6d
pkgrel=1
pkgdesc='gSSO glib daemon'
arch=('x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('glib2' 'glibc' 'sqlite')
makedepends=('git' 'gobject-introspection' 'gtk-doc' 'vala')
provides=('gsignond' 'libgsignond-common.so')
conflicts=('gsignond')
backup=('etc/gsignond.conf')
source=("git+https://gitlab.com/accounts-sso/gsignond.git")
sha256sums=('SKIP')

pkgver() {
  cd gsignond

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  cd gsignond

unset CPPFLAGS

  ./autogen.sh \
    --prefix='/usr' \
    --sysconfdir='/etc' \
    --enable-dbus-type='session'
  make
}

package() {
  cd gsignond

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
