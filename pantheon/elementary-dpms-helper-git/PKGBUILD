# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-dpms-helper-git
pkgver=r2.601b7f0
pkgrel=1
pkgdesc='DPMS helper for Pantheon'
arch=('any')
url='https://github.com/elementary/dpms-helper'
license=('GPL')
groups=('pantheon-unstable')
makedepends=('git' 'meson')
provides=('elementary-dpms-helper')
conflicts=('elementary-dpms-helper')
source=('elementary-dpms-helper::git+https://github.com/elementary/dpms-helper.git')
sha256sums=('SKIP')

pkgver() {
  cd elementary-dpms-helper

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  arch-meson ../elementary-dpms-helper
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
}

# vim: ts=2 sw=2 et:
